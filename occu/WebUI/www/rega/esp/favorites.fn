function ::WriteEndDevices()
{
  boolean bHasDevice = false;
  string endDevices = "";
  if( oPC )
  {
    string s;
    foreach(s,oPC.EnumIDs())
    {
      if( s == oOrigFav.ID() )
      {
        bHasDevice = true;
        endDevices = "PC";
        Write( "PC" );
      }
    }
  }  
  if( oPDA )
  {
    string s;
    foreach(s,oPDA.EnumIDs())
    {
      if( s == oOrigFav.ID() )
      {
        if( bHasDevice ) { Write( "<br />" ); }
        bHasDevice = true;
        endDevices = endDevices # "PDA";
        Write( "PDA" );
      }
    }
  }
  if( oCentral )
  {
    string s;
    foreach(s,oCentral.EnumIDs())
    {
      if( s == oOrigFav.ID() )
      {
        if( bHasDevice ) { Write( "<br />" ); }
        endDevices = endDevices # "Zentrale";
        Write( "Zentrale" );
      }
    }
  }
  return;
}

function ::WriteUsers()
{
  object oUsers = dom.GetObject( ID_USERS );
  object oFavorites = dom.GetObject( ID_FAVORITES );
  boolean bHasUser = false;
  if( oUsers && oFavorites )
  {
    string s;
    foreach( s, oUsers.EnumEnabledVisibleIDs() )
    {
      object oUserItem = oFavorites.Get( "_USER"#s );
      if( oUserItem )
      {
        object oFavTmp = oUserItem.Get( oOrigFav.ID() );
        if( oFavTmp )
        {
          object oUTmp = dom.GetObject( s );
          if( oUTmp )
          {
            favUserIds = favUserIds # oUTmp.ID()#"\t";
            if( bHasUser ) { Write( "<br />" ); }
            string sUserFullName = oUTmp.UserFirstName()#' '#oUTmp.UserLastName();
            if( sUserFullName == ' ' ){ sUserFullName = oUTmp.Name(); }
            Write( sUserFullName );
            bHasUser = true;
          }
        }
      }
    }
  }
  return;
}

function ::AddToStatus()
{
  if( system.IsVar("id") && system.IsVar("cid") )
  {
    object oFav = dom.GetObject( system.GetVar("id") );
    if( oFav )
    {
      oFav.FavControlIDs().Add( system.GetVar("cid") );
    }
  }
}

function ::RemoveFromStatus()
{
  if( system.IsVar("id") && system.IsVar("cid") )
  {
    object oFav = dom.GetObject( system.GetVar("id") );
    if( oFav )
    {
      oFav.FavControlIDs().Remove( system.GetVar("cid") );
    }
  }
}

function ::CopyToNew()
{
  if( system.IsVar("flid") && system.IsVar("bIsPC") && system.IsVar("bIsPDA") && system.IsVar("bIsCENTRAL") && system.IsVar("sargs") )
  {
    integer iFavID = system.GetVar("flid");
    object oCopy = dom.GetObject( iFavID );
    if( oCopy )
    {
      Write( oCopy.EnCopyID( ID_ERROR ) );
      string sUniqueName;
      dom.CheckName(oCopy.Name(), &sUniqueName, oCopy.ID() );
      oCopy.Name( sUniqueName );
            
      object oPC = dom.GetObject( 202 );
      if( oPC )
      {
        if( system.GetVar("bIsPC")=="true" ) { oPC.Add( iFavID ); }
      }
      
      object oPDA = dom.GetObject( 203 );
      if( oPDA )
      {
        if( system.GetVar("bIsPDA")=="true" ) { oPDA.Add( iFavID ); }
      }
      
      object oCENTRAL = dom.GetObject( 204 );
      if( oCENTRAL )
      {
        if( system.GetVar("bIsCENTRAL")=="true" ) { oCENTRAL.Add( iFavID ); }
      }
      
      object oUsers = dom.GetObject( ID_USERS );
      object oFavorites = dom.GetObject( ID_FAVORITES );
      
      string s;
      foreach(s,oUsers.EnumEnabledVisibleIDs())
      {
        object oUser = dom.GetObject( s );
        if( oUser )
        {
          object oU = oFavorites.Get( "_USER"#s );
          if( !oU )
          {
            oU = dom.CreateObject( OT_FAVORITE, "_USER"#s );
            oFavorites.Add( oU );
          }
        }
      }
      
      Write( system.GetVar("sargs") );
      
      foreach(s,system.GetVar("sargs"))
      {
        object oU = oFavorites.Get( "_USER"#s );
        if( oU )
        {
          oU.Add( iFavID );
          Write( "|add "#iFavID#" to "#oU.ID()#"|" );
        }
      }
      
    }
  }  
  return;
}

function ::MovePosition()
{
  if( system.IsVar("flid") && system.IsVar("id") && system.IsVar("dir") )
  {
    object oFLTmp = dom.GetObject( system.GetVar("flid") );
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oFLTmp && oTmp )
    {
      if( system.GetVar("dir") == "+" )
      {
        Write( oFLTmp.MoveObject( oTmp.ID(), 1 ) );
      }
      if( system.GetVar("dir") == "-" )
      {
        Write( oFLTmp.MoveObject( oTmp.ID(), -1 ) );
      }
    }
  }
}

function ::SaveFavoriteList()
{
  if( system.IsVar("flid") && system.IsVar("bIsPC") && system.IsVar("bIsPDA") && system.IsVar("bIsCENTRAL") && system.IsVar("sargs") )
  {
    integer iFavID = system.GetVar("flid");
    object oCopy = dom.GetObject( iFavID );
    if( oCopy )
    {
      integer iOrigID = oCopy.EnCopyID();
      object oOrig = dom.GetObject( iOrigID );
      if( oOrig )
      {
        oCopy.EnumCopyTo( oOrig.ID() );
        dom.DeleteObject( oCopy.ID() );
        oOrig.EnCopyID( ID_ERROR );
      }
      else
      {
        iOrigID = oCopy.ID();
      }
      
      Write( system.GetVar("bIsPC") );
      object oPC = dom.GetObject( 202 );
      if( oPC )
      {
        oPC.Remove( iOrigID );
        if( system.GetVar("bIsPC")=="true" ) { oPC.Add( iOrigID ); }
      }
      object oPDA = dom.GetObject( 203 );
      if( oPDA )
      {
        oPDA.Remove( iOrigID );
        if( system.GetVar("bIsPDA")=="true" ) { oPDA.Add( iOrigID ); }
      }
      object oCENTRAL = dom.GetObject( 204 );
      if( oCENTRAL )
      {
        oCENTRAL.Remove( iOrigID );
        if( system.GetVar("bIsCENTRAL")=="true" ) { oCENTRAL.Add( iOrigID ); }
      }
      
      object oUsers = dom.GetObject( ID_USERS );
      object oFavorites = dom.GetObject( ID_FAVORITES );
      
      string s;
      foreach(s,oUsers.EnumEnabledVisibleIDs())
      {
        object oUser = dom.GetObject( s );
        if( oUser )
        {
          object oU = oFavorites.Get( "_USER"#s );
          if( !oU )
          {
            oU = dom.CreateObject( OT_FAVORITE, "_USER"#s );
            oFavorites.Add( oU );
          }
          else
          {
            oU.Remove( iOrigID );
            Write( "|rem "#iOrigID#" from "#oU.ID()#"|" );
          }
        }
      }
      
      Write( system.GetVar("sargs") );
      
      foreach(s,system.GetVar("sargs"))
      {
        object oU = oFavorites.Get( "_USER"#s );
        if( oU )
        {
          oU.Add( iOrigID );
          Write( "|add "#iOrigID#" to "#oU.ID()#"|" );
        }
      }
    }
  }
}

function ::AddSeparator()
{
  if( system.IsVar("flid") )
  {
    object oFavList = dom.GetObject( system.GetVar("flid") );
    if( oFavList )
    {
      object oTmp = dom.CreateObject( OT_OBJECT );
      if( oTmp )
      {
        Write( oFavList.Add( oTmp.ID() ) );
      }
    }
  }
  return;
}

function ::AddToList()
{
  Write( system.GetVar("flid") );
  Write( "|" );
  Write( system.GetVar("id") );
  if( system.IsVar("flid") && system.IsVar("id") )
  {
    object oFavList = dom.GetObject( system.GetVar("flid") );
    if( oFavList )
    {
      object oTmp = dom.GetObject( system.GetVar("id") );
      if( oTmp )
      {
        Write( oFavList.Add( oTmp.ID() ) );
      }
    }
  }
  return;
}

function ::RemoveFromList()
{
  if( system.IsVar("flid") && system.IsVar("id") )
  {
    object oFavList = dom.GetObject( system.GetVar("flid") );
    if( oFavList )
    {
      object oTmp = dom.GetObject( system.GetVar("id") );
      if( oTmp )
      {
        Write( oFavList.Remove( oTmp.ID() ) );
        if( oTmp.Type() == OT_OBJECT )
        {
          dom.DeleteObject( oTmp.ID() );
        }
      }
    }
  }
  return;
}

function ::AddList()
{
  if( system.IsVar("fname") )
  {
    object oFavorites = dom.GetObject( ID_FAVORITES );
    if( oFavorites )
    {
      object oFavList = dom.CreateObject( OT_FAVORITE, system.GetVar("fname") );
      if( oFavList )
      {
        oFavList.EnumType( etFavorite );
        oFavorites.Add( oFavList.ID() );
      }
    }
  }
  return;
}

function ::RemoveList()
{
  Write("RemoveList|");
  if( system.IsVar("flid") )
  {
    Write("PARAM=OK|");
    object oFavorites = dom.GetObject( ID_FAVORITES );
    if( oFavorites )
    {
      Write("ID_FAVORITES=OK|");
      object oFavList = dom.GetObject( system.GetVar("flid") );
      if( oFavList )
      {
        Write("FAV_LIST=OK|");
        if( oFavorites.Remove( oFavList.ID() ) )
        {
          Write("FAV_LIST=REMOVED|");
          Write("FAV_LIST_DELETED=");
          if( !oFavList.Unerasable() )
          {
            Write( dom.DeleteObject( oFavList.ID() ) );
          }
          Write("|");
        }
      }
    }
  }
  return;
}

function ::SetFavoriteList()
{
  if( system.IsVar("id") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      object oCopyTmp = oTmp.EnumCopyTo();
      if( oCopyTmp )
      {
        dom.GetObject( ID_FAVORITES ).Add( oCopyTmp.ID() );
        string sTmp = "";
        oCopyTmp.Name( oTmp.Name(), &sTmp, oTmp.ID() );
        system.SetSessionVar("sessionFAVID", oCopyTmp.ID());
        system.SetSessionVar("sessionORIGFAVID", oTmp.ID());
        Write( system.GetSessionVar("sessionFAVID")#"|"#system.GetSessionVar("sessionORIGFAVID") );
      }
      else
      {
        Write( "FALSE" );
      }
    }
  }
  return;
}

function ::NewFavoriteList()
{
  object oFTmp = dom.GetObject( ID_FAVORITES );
  object oTmp = dom.CreateObject( OT_FAVORITE );
  if( oTmp && oFTmp )
  {
    object oPC = dom.GetObject( 202 );
    if( oPC )
    {
      oPC.Add( oTmp.ID() );
    }
    object oU = oFTmp.Get( "_USER"#system.GetSessionVar("sessionUserID") );
    if( oU )
    {
      oU.Add( oTmp.ID() );
    }
    !oTmp.Name( "Neue Favoritenliste" );
    oTmp.Name( "${createNewFavoriteProposal}" );
    !Write('<script type="text/javascript">translatePage("#PagePath");</script>');
    oFTmp.Add( oTmp.ID() );
    system.SetSessionVar("sessionFAVID", oTmp.ID());
    system.SetSessionVar("sessionORIGFAVID", oTmp.ID());
    Write( "true|" );
    Write( system.GetSessionVar("sessionFAVID")#"|"#system.GetSessionVar("sessionORIGFAVID") );
  }
  return;
}

function ::FavColumnCount()
{
  if( system.IsVar("id") )
  {
    object oFavorite = dom.GetObject( system.GetVar("id") );
    if( oFavorite )
    {
      if( system.IsVar("count") )
      {
        Write( oFavorite.FavColumnCount( system.GetVar("count") ) );
      }
      else
      {
        Write( oFavorite.FavColumnCount() );
      }
    }
  }
}

function ::FavColumnAlign()
{
  if( system.IsVar("id") )
  {
    object oFavorite = dom.GetObject( system.GetVar("id") );
    if( oFavorite )
    {
      if( system.IsVar("align") )
      {
        Write( oFavorite.FavColumnAlign( system.GetVar("align") ) );
      }
      else
      {
        Write( oFavorite.FavColumnAlign() );
      }
    }
  }
}

function ::FavNamePosition()
{
  if( system.IsVar("id") )
  {
    object oFavorite = dom.GetObject( system.GetVar("id") );
    if( oFavorite )
    {
      if( system.IsVar("pos") )
      {
        Write( oFavorite.FavNamePosition( system.GetVar("pos") ) );
      }
      else
      {
        Write( oFavorite.FavNamePosition() );
      }
    }
  }
}