!*******************************************************************************
!* session.fn
!* Hilfsfunktionen zur Session-Verwaltung.
!*
!* Autor      : F. Werner
!* Erstellt am: 22.05.2008
!*******************************************************************************

!*******************************************************************************
!* session.fn::GetUserId()
!* Ermittelt die Id des Benutzers anhand der SessionId.
!*
!* Parameter:
!*   sessionId: aktuelle SessionId
!*
!* Rückgabewert:
!*   userId: Id des Benutzers oder "", falls die SessionId nicht angegeben oder
!*           ungültig ist.
!*******************************************************************************
function ::GetUserId()
{
  var userId = "";
  string sUser  = system.GetSessionVarStr(sessionId);
  if ("" != sUser)
  {
    userId = sUser.StrValueByIndex(";", 0);
  }
}

!*******************************************************************************
!* session.fn::GetUPL()
!* Ermittelt die Nutzerrechte (UPL = User Privilege Level) anhand der
!* SessionId.
!*
!* Parameter:
!*   sessionId: akutelle SessionId
!*
!* Rückgabewert:
!*   upl: Nutzerechte oder 0, falls die SessionId nicht angegeben oder ungültig
!*        ist.
!*******************************************************************************
function ::GetUPL()
{
  var upl = 0;
  string sUser = system.GetSessionVarStr(sessionId);
  if ("" != sUser)
  {
    var sUPL = sUser.StrValueByIndex(";", 1);
    upl      = sUPL.ToInteger(); 
  }  
}

!*******************************************************************************
!* Prüft, ob eine die für eine Anfrage benötigte Privilegstufe vorhanden ist.
!*
!* Paramter (globale Variablen, MÜSSEN angegeben sein):
!*   request: Name der Anfrage
!*   level  : erforderliche Privilegstufe
!*******************************************************************************
function ::checkRequest()
{
  var upl;
  Call("/xml-api/session.fn::GetUPL()");
  
  if (level > upl)
  {
    WriteLine("<?" # "xml version=\"1.0\" encoding=\"ISO-8859-1\" ?>");
    WriteLine("<"  # request # ">");
    WriteLine("</" # request # ">");
    quit;
  }
}